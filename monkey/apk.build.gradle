/*
 * Copyright (c) 2020 Bytedance Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
/**
 * @author Yuhui Su 
 */
//if you want to package this project as an apk, use this script instead of the current build.gradle
//build apk with:
//  ./gradlew assembleRelease
//
//please push the built apk to /data/local/tmp and name the apk as monkey.apk:
//  adb push monkey/build/outputs/apk/release/monkey-release-unsigned.apk /data/local/tmp/monkey.apk
//
//run with:
//
//  adb shell CLASSPATH=/data/local/tmp/monkey.apk exec app_process /system/bin com.android.commands.monkey.Monkey -p package_name --agent reuseq --running-minutes duration(min) --throttle delay(ms) -v -v

apply plugin: 'com.android.application'

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    buildTypes {
        debug{
            debuggable true
        }
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    externalNativeBuild {
        cmake {
            path("../native/CMakeLists.txt")
            version = "3.18.1"
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
    }
    ndkVersion '25.2.9519653'

}

repositories {
    maven {
        url "https://dl.google.com/dl/android/maven2/"
    }
}


dependencies {
    compileOnly files('libs/framework.jar')
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.vdurmont:emoji-java:5.0.0'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile).tap {
        configureEach {
            Set<File> fileSet = options.bootstrapClasspath.getFiles()
            List<File> newFileList = new ArrayList<File>()
            newFileList.add(new File("libs/framework.jar"))
            newFileList.addAll(fileSet)
            options.bootstrapClasspath = files(newFileList.toArray())
        }
    }
}
